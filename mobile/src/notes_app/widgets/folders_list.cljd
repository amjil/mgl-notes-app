(ns notes-app.widgets.folders-list
   (:require
    ["package:flutter/material.dart" :as m]
    ["package:mongol/mongol.dart" :as mgl]
    [cljd.flutter.alpha2 :as f]
    [notes-app.states.global :as gs]
    [notes-app.widgets.slidable :as slidable]
    [notes-app.screens.folder :as folder-screen]))

 (defn list-item [i item router-fn]
   (f/widget
    :get [m/Navigator]
    (m/GestureDetector
     .onTap (fn []
              (swap! gs/state assoc :folder item)
              (router-fn)
              nil)
     .child
     (slidable/folder
      i
      item
      (m/Container
       .width 40
       .child
       (m/Column
        .mainAxisAlignment m.MainAxisAlignment/center
        .children [(m/Expanded
                    .child (m/Column
                            .children
                            [(m/Icon m.Icons/folder)
                             (m/SizedBox .height 10)
                             (mgl/MongolText
                              (get item "name")
                              .maxLines 1
                              .softWrap true
                              .overflow m.TextOverflow/ellipsis
                              .style (m/TextStyle
                                      .fontSize 20))]))
                   (m/Padding
                    .padding (m.EdgeInsets/only .bottom 10)
                    .child
                    (m/Column
                     .children [(mgl/MongolText (str (get item "related_num")))
                                (m/Icon m.Icons/keyboard_arrow_down_outlined)]))]))))))

(defn list-view [router-fn]
  (f/widget
   :context ctx
   :watch [{folders :folders} gs/state]
   (m/SingleChildScrollView
    .scrollDirection m.Axis/horizontal)
   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(m/Padding
      .padding (m.EdgeInsets/symmetric .horizontal 10)
      .child
      (mgl/MongolText "Folders"
                      .style (m/TextStyle
                              .fontSize 20
                              .color (-> m/Theme (.of ctx) .-colorScheme .-primary)
                              .fontWeight m.FontWeight/w600)))
     (m.ListView/separated
      .scrollDirection m.Axis/horizontal
      .physics (m/NeverScrollableScrollPhysics)
      .separatorBuilder (fn [context i]
                          (m/VerticalDivider
                           .color (-> m/Theme (.of ctx) .-colorScheme .-primary)))
      .itemCount (count folders)
      .itemBuilder (fn [context i]
                     (list-item i (nth folders i) router-fn))
      .shrinkWrap true)])))