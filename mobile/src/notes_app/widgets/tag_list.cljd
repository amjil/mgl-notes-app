(ns notes-app.widgets.tag-list
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [notes-app.widgets.menu-buttons :as menu-buttons]
   [notes-app.states.global :as gs]
   [notes-app.states.tag :as tag-state]
   [notes-app.utils.util :as util]))
   
(defn list-item [item]
  (f/widget
   :context ctx
   :get [m/Navigator]
   (m/GestureDetector
    .onTap (fn [] 
             (.pushNamed navigator "/tag")
             (swap! gs/state assoc-in [:tag :filter (get item "id")] 1)
             (tag-state/tag-filterd-search)
             nil)
    .onLongPress (fn []
                   (util/show-tag-menus
                    ctx
                    (menu-buttons/tag item))))
   (m/Container
    .padding (m.EdgeInsets/symmetric .vertical 8 .horizontal 4)
    .margin (m.EdgeInsets/only .bottom 8)
    .decoration (m/BoxDecoration
                 .borderRadius (.all m/BorderRadius (.circular m/Radius 10))
                 .border (.all m/Border .width 0.1)
                 .color (-> m/Theme (.of ctx) .-colorScheme .-secondaryContainer))

    .child (mgl/MongolText (str "#" (get item "name"))
                           .maxLines 1
                           .softWrap true
                           .overflow m.TextOverflow/ellipsis
                           .style (m/TextStyle
                                   .fontSize 20
                                   .color (-> m/Theme (.of ctx) .-colorScheme .-primary)
                                   .fontWeight m.FontWeight/w600)))))
   
(defn list-view []
  (f/widget
   :context ctx
   :watch [{tags :tags} gs/state]
   (m/SingleChildScrollView
    .scrollDirection m.Axis/horizontal)
   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(mgl/MongolText "Tags"
                     .style (m/TextStyle
                             .fontSize 20

                             .color (-> m/Theme (.of ctx) .-colorScheme .-primary)
                             .fontWeight m.FontWeight/w600))
     (m/SizedBox .width 10)
     (m/Padding
      .padding (m.EdgeInsets/all 8)
      .child
      (m/Wrap
       .direction m.Axis/vertical
       .runSpacing 4
       .spacing 4
       .children
       (map #(list-item %) tags)))])))