(ns notes-app.widgets.mgl-input-editor
  (:require [cljd.flutter :as f]
            [virtual-keyboard.keyboard :as keyboard]
            [menu-bar.menu :as menu]
            [virtual-keyboard.options :as options]
            [virtual-keyboard.input-control :as control]
            [virtual-keyboard.keyboard-action :as keyboard-action]
            ["package:mongol/mongol.dart" :as mgl]
            ["package:flutter/material.dart" :as m]))

(defn input-editor
  [{:keys [controller on-changed autofocus? placeholder]}]
  (f/widget
   :context ctx
   :get [:info :state]
   :let [width (-> m/MediaQuery (.of ctx) .-size .-width)
         theme (-> m/Theme (.of ctx))
         color-scheme (.-colorScheme theme)]
   :watch [{candidates-list :keyboard/candidates-list} state
           visible control/visible]
   (m/SafeArea
    .child
    (m/Center
     .child
     (m/Stack
      .children
      [(m/Column
        .children
        [(m/Expanded
          .child
          (mgl/MongolTextField
           .controller controller
           .maxLines 500
           .autofocus (boolean autofocus?)
           .onChanged (fn [val] (when on-changed (on-changed val)))
           .onTap (fn []
                    (swap! state assoc :keyboard/candidates-list [])
                    (dart:core/print "onTap MongolTextField"))
           .style (m/TextStyle 
                   .fontSize 20
                   .color (.-onSurface color-scheme))
           .decoration (m/InputDecoration 
                        .border (m/OutlineInputBorder)
                        .fillColor (.-surface color-scheme)
                        .filled true)))
         (m/Visibility
          .visible visible
          .child
          (m/FocusScope
           .canRequestFocus false
           .child
           (m/TextFieldTapRegion
            .child
            (m/Container
             .color (.-surfaceVariant color-scheme)
             .child
             (keyboard/keyboard {:custom-fns {}})))))])
       (m/Positioned
        .bottom (+ options/keyboard-default-height (* 4 (+ 4 (:keyboard/row-vertical-padding info))))
        .right (/ width 4)
        .child
        (menu/menu {:bar {:elavation 10}
                    :item {:on-tap (fn [x] (keyboard-action/on-candidates-clicked x state))}}
                   candidates-list))])))))