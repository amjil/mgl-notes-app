(ns notes-app.widgets.mgl-setting
  (:require 
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]))

(declare settings-item)

(defn settings-group [items]
  (f/widget
   :context ctx
   (m/Container
    .margin (m.EdgeInsets/only .left 20)
    .decoration (m/BoxDecoration
                 .color (-> m/Theme (.of ctx) .-cardColor)
                 .borderRadius (m.BorderRadius/circular 15))
    .child (if (empty? items)
             (m/Center
              .child (mgl/MongolText "No settings available"))
             (m.ListView/separated
              .scrollDirection m.Axis/horizontal
              .separatorBuilder (fn [context i]
                                  (m/VerticalDivider))
              .itemCount (count items)
              .itemBuilder (fn [context i]
                             (settings-item (get items i)))
              .shrinkWrap true
              .padding m.EdgeInsets/zero
              .physics (m/ScrollPhysics))))))

(defn settings-item-content [item]
  (f/widget
   :context ctx
   (m/ClipRRect
    .borderRadius (m.BorderRadius/circular 15)
    .child (mgl/MongolListTile
            .leading (when (:leading item)
                       (m/Padding
                        .padding (m.EdgeInsets/all 5)
                        .child (m/Icon (:leading item))))
            .title (mgl/MongolText (or (:title item) "Untitled"))
            .subtitle (when (:subtitle item)
                        (mgl/MongolText (:subtitle item)))
            .trailing (:trailing item)))))

(defn settings-item [item]
  (when item
    (if (:on-tap item)
      (m/GestureDetector
       .onTap (fn [] ((:on-tap item)))
       .child (settings-item-content item))
      (settings-item-content item))))
