(ns notes-app.widgets.app-bar
  (:require
   ["package:flutter/material.dart" :as m]
   [notes-app.states.ui :as ui]
   [notes-app.utils.navigator :as navigator]
   [notes-app.utils.note :as note-utils]
   [notes-app.states.notes :as notes]))

(defn mgl-app-bar [navigator search-route]
  (m/AppBar
   .backgroundColor m/Colors.transparent
   .leading (m/IconButton
             .icon (m/Icon m/Icons.menu)
             .onPressed (fn [] (.openDrawer (.-currentState ui/scaffold-key))))
   ;;  .title (m/Text "Minii Notes"
   ;;                 .style (m/TextStyle .fontSize 22))
   .actions [(m/IconButton
              .icon (m/Icon (m/Icons.search))
              .onPressed (fn [] (navigator/navigate-to-screen navigator search-route)))]))

(defn mgl-search-app-bar [navigator]
  (m/AppBar
   .backgroundColor m/Colors.transparent
   .leading (m/IconButton
             .icon (m/Icon m/Icons.arrow_back)
             .onPressed (fn [] (navigator/navigate-to-back navigator)))))

(defn mgl-editor-app-bar [ctx navigator]
  (m/AppBar
   .backgroundColor m/Colors.transparent
   .leading (m/IconButton
             .icon (m/Icon m/Icons.arrow_back)
             .onPressed (fn []
                          ;; Save current note first, then restore previous note from history if available
                          (let [current-note (notes/get-current-note)]
                            (if current-note
                              ;; Save current note, then restore previous and navigate back
                              (-> (note-utils/save-or-update-note! ctx)
                                  (.then (fn [_]
                                           (let [previous-note (notes/restore-previous-note!)]
                                             (when previous-note
                                               (note-utils/load-note-to-editor!))
                                             (navigator/navigate-to-back navigator)))))
                              ;; No current note, just navigate back
                              (let [previous-note (notes/restore-previous-note!)]
                                (when previous-note
                                  (note-utils/load-note-to-editor!))
                                (navigator/navigate-to-back navigator))))))
   .actions [(m/IconButton
              .icon (m/Icon m/Icons.save)
              .onPressed (fn []
                           (note-utils/save-or-update-note! ctx)))]))