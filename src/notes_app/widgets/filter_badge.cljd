(ns notes-app.widgets.filter-badge
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [notes-app.states.notes :as note-state]))

(defn filter-badge []
  (f/widget
   :context ctx
   :watch [{filter-tag-name :filter-tag-name
            filter-date :filter-date} note-state/notes-state]
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (-> theme .-colorScheme)
         text-color (-> color-scheme .-primary)
         bg-color (-> color-scheme .-primaryContainer)
         has-filter? (or (not-empty filter-tag-name) (not-empty filter-date))
         clear-filter (fn []
                       (note-state/clear-filter!)
                       (note-state/load-notes-paginated! ctx 0))]
   (m/Container
    .padding (m/EdgeInsets.symmetric .vertical 1 .horizontal 1)
    .decoration (m/BoxDecoration
                 .color bg-color)
    .child
    (m/Column
     .mainAxisSize m/MainAxisSize.min
     .mainAxisAlignment m/MainAxisAlignment.start
     .crossAxisAlignment m/CrossAxisAlignment.center
     .children [(m/Column
                 .mainAxisSize m/MainAxisSize.min
                 .mainAxisAlignment m/MainAxisAlignment.start
                 .children [(m/Icon
                             (m/Icons.filter_alt_rounded)
                             .size 16
                             .color text-color)
                            (mgl/MongolText  (cond
                                               (not-empty filter-tag-name) filter-tag-name
                                               (not-empty filter-date) filter-date
                                               :else "All Notes")
                                             .style (m/TextStyle .fontSize 14
                                                                 .fontWeight (m/FontWeight.w500)
                                                                 .color text-color))])
                (if has-filter?
                  (m/GestureDetector
                   .onTap clear-filter
                   .child (m/Container
                           .padding (m/EdgeInsets.all 4)
                           .child (m/Icon
                                   m/Icons.close
                                   .size 16
                                   .color text-color)))
                  (m/SizedBox))]))))
(defn positioned-filter-badge []
  (m/Positioned
   .top 0
   .right 0
   .child (filter-badge)))