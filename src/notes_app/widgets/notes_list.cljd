(ns notes-app.widgets.notes-list
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [notes-app.widgets.note-item :as note-item]
   [notes-app.widgets.infinite-scroll :as infinite-scroll]
   [notes-app.widgets.empty-state :as empty-state]
   [notes-app.widgets.loading-state :as loading-state]
   [notes-app.widgets.filter-badge :as filter-badge]
   [notes-app.states.notes :as notes]
   [notes-app.utils.toast :as toast]))

;; ==================== Notes list specific functions ====================

;; Check if should trigger load more for notes
(defn should-load-more-notes? []
  (and (:has-more @notes/notes-state)
       (not (:loading-more @notes/notes-state))))

;; Load more notes trigger function
(defn load-more-notes-trigger [ctx]
  (when (should-load-more-notes?)
    (notes/load-notes-paginated! ctx (inc (notes/get-current-page)) (notes/get-page-size))))

;; Note item builder
(defn note-item-builder [note]
  (note-item/note-item note))

;; ==================== Notes list widget ====================

;; Notes list with infinite scroll
(defn notes-list []
  (f/widget
   :context ctx
   :watch [{notes :results
            loading :loading
            loading-more :loading-more
            has-more :has-more
            current-page :current-page
            page-size :page-size} notes/notes-state]
   :managed [scroll-controller (infinite-scroll/create-scroll-controller (fn [] (load-more-notes-trigger ctx)))]
   (m/Stack
    .children
    [(if loading
       (loading-state/loading-indicator-themed)
       (if (empty? notes)
         (empty-state/empty-notes)
         (infinite-scroll/infinite-scroll-list
          {:items notes
           :scroll-controller scroll-controller
           :scroll-direction m/Axis.horizontal
           :item-builder note-item-builder
           :loading-more? loading-more
           :has-more? has-more
           :load-more-indicator (infinite-scroll/load-more-indicator infinite-scroll/loading-more-text)
           :all-loaded-indicator (infinite-scroll/all-loaded-indicator infinite-scroll/all-loaded-text)})))
     (filter-badge/positioned-filter-badge)])))

;; ==================== Container widget ====================

;; Notes list container
(defn notes-list-container []
  (f/widget
   (m/Expanded
    .child (notes-list))))

(defn notes-list-stream []
  (f/widget
   :context ctx
   :watch [widget-type
           (stream
            (map (fn [notes]
                   (notes/handle-notes-result-for-stream notes)
                   :list-widget))
            (map (fn [[er st]]
                   (toast/handle-error-with-toast ctx "Error while fetching notes")
                   :error-widget))
            :as-values (notes/get-notes-for-stream))
           :default :progress-widget]
   (case widget-type
     :list-widget (notes-list)
     :error-widget (empty-state/empty-notes-with-errors)
     :progress-widget (loading-state/loading-indicator-themed))))