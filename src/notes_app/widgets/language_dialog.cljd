(ns notes-app.widgets.language-dialog
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [notes-app.states.locale :as locale-state]
   [notes-app.utils.i18n :as i18n]))

(defn- language-option-tile [ctx option selection]
  (m/RotatedBox
   .quarterTurns 1
   .child (m/RadioListTile
           .title (m/Text (locale-state/option-label ctx option))
           .value (:selection option)
           .groupValue selection
           .onChanged (fn [value]
                        (locale-state/set-selection! value)
                        (m/Navigator.pop ctx)))))

(defn language-dialog [_context]
  (f/widget
   :context ctx
   :watch [_locale locale-state/locale-state]
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (.-colorScheme theme)
         selection (locale-state/get-selection)
         options (locale-state/available-options)
         option-widgets (vec (map (fn [option]
                                    (language-option-tile ctx option selection))
                                  options))]
   (m/AlertDialog
    .content
    (m/Row
     .children
     [(mgl/MongolText (i18n/t ctx :language/dialog-title)
                      .style (m/TextStyle
                              .fontSize 20
                              .fontWeight m/FontWeight.bold
                              .color (.-onSurface color-scheme)))
      (m/Container
       .child (m/SingleChildScrollView
               .scrollDirection m/Axis.horizontal
               .child
               (m/Row
                .mainAxisSize m/MainAxisSize.min
                .children option-widgets)))]))
    .actions
    [(m/TextButton
      .onPressed (fn [] (m/Navigator.pop ctx))
      .child (mgl/MongolText (i18n/t ctx :dialog/close)))]))

(defn show-language-dialog [context]
  (m/showDialog
   .context context
   .builder (fn [ctx] (language-dialog ctx))))

