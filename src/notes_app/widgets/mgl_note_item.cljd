(ns notes-app.widgets.mgl-note-item
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [notes-app.widgets.mgl-popup-menu :as popup-menu]
   [notes-app.utils.clipboard :as clipboard-utils]
   [notes-app.states.notes :as notes]
   [notes-app.states.font :as font-state]
   [notes-app.utils.note :as note-util]
   [notes-app.utils.navigator :as navigator]))

;; Create generic menu item function
(defn create-note-menu-items [navigator ctx note]
  [(popup-menu/mongol-menu-item
    {:label "Засах"
     :icon m/Icons.edit
     :on-selected (fn []
                    (notes/set-current-note! note)
                    (note-util/load-note-to-editor!)
                    (navigator/navigate-to-screen navigator "/editor"))})
   (popup-menu/mongol-menu-item
    {:label "Хуулах"
     :icon m/Icons.copy
     :on-selected (fn []
                    (clipboard-utils/set-data ctx (get note "content")))})
   (popup-menu/mongol-menu-item-divider)
   (popup-menu/mongol-menu-item
    {:label (if (zero? (get note "favorite")) "Favorite" "Unfavorite")
     :icon m/Icons.copy
     :on-selected (fn []
                    (if (zero? (get note "favorite"))
                      (note-util/favorite (get note "id"))
                      (note-util/unfavorite (get note "id"))))})
   (popup-menu/mongol-menu-item-divider)
   (popup-menu/mongol-menu-item
    {:label "Устгах"
     :icon m/Icons.delete
     :on-selected (fn []
                    (notes/delete-note! ctx (get note "id")))})])

;; Function to show popup menu
(defn show-note-popup-menu [navigator ctx details note]
  (let [menu-items (create-note-menu-items navigator ctx note)]
    (popup-menu/show-menu
     {:context ctx
      :items menu-items
      :detail details
      :on-item-selected (fn [menu-item]
                          (dart:core/print (str "Menu item selected: " (get menu-item :label))))})))

(defn note-item [item]
  (f/widget
   :context ctx
   :get [m/Navigator]
   :watch [_note-font-state font-state/note-font-state]
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (.-colorScheme theme)
         is-dark? (= (.-brightness color-scheme) m/Brightness.dark)
         bg-color (.-surface color-scheme)
         title-color (.-onSurface color-scheme)
         preview-color (.-onSurfaceVariant color-scheme)
         border-color (.-outline color-scheme)
         shadow-color (.-shadow color-scheme)
         font-family (font-state/get-note-font-family)
         font-size (font-state/get-note-font-size)
         item (.-data item)
         note-id (get item "id")
         note-title (get item "title")
         note-content (get item "content")
         title-index (if (nil? note-content)
                       -1
                       (.indexOf note-content "\n"))
         note-content (if (> title-index 0)
                        (subs note-content title-index)
                        nil)]
   (m/Card
    .color bg-color
    .margin (m/EdgeInsets.all 8)
    .shape (m/RoundedRectangleBorder
            .borderRadius (m/BorderRadius.circular 16)
            .side (m/BorderSide .color border-color .width 1))
    .elevation (if is-dark? 2 4)
    .shadowColor shadow-color)
   (m/Padding
    .padding (m/EdgeInsets.all 12))
   (m/Column
    .children
    [(popup-menu/mongol-popup-menu-button
      {:items (create-note-menu-items navigator ctx item)
       :on-selected (fn [menu-item]
                      (dart:core/print (str "Menu item selected: " (get menu-item :label))))})

     (m/SizedBox .width 8)
     (m/Expanded
      .child
      (m/GestureDetector
       .onLongPressStart (fn [details] (show-note-popup-menu navigator ctx details item))
       .onDoubleTap (fn []
                      (dart:core/print (str "Note item double tapped: " note-id))
                      (notes/set-current-note! item)
                      (note-util/load-note-to-editor!)
                      (navigator/navigate-to-screen navigator "/editor"))
       .onTap (fn []
                (dart:core/print (str "Note item tapped: " note-id))
                (notes/set-current-note! item)
                (note-util/load-note-to-editor!)
                (navigator/navigate-to-screen navigator "/editor"))
       .child
       (mgl/MongolListTile
        .title (when note-title
                 (mgl/MongolText note-title
                                 .style (m/TextStyle
                                         .fontFamily font-family
                                         .fontSize font-size
                                         .fontWeight m/FontWeight.bold
                                         .color title-color
                                         .height 1.2)))
        .subtitle (when note-content
                    (mgl/MongolText note-content
                                    .maxLines 3
                                    .style (m/TextStyle
                                            .fontFamily font-family
                                            .fontSize (- font-size 4)
                                            .color preview-color
                                            .height 1.4))))))])))