(ns notes-app.widgets.mgl-stats-widget
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [notes-app.states.stats :as stats]))

;; Data validation function
(defn validate-stat-value [value]
  (if (and (number? value) (>= value 0))
    value
    0))

;; Individual stat item component
(defn- stat-item [label value]
  (f/widget
   :context ctx
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (-> theme .-colorScheme)
         text-color (-> color-scheme .-primary)]
   (m/Expanded
    .child
    (m/Column
     .mainAxisAlignment m/MainAxisAlignment.spaceBetween
     .children [(mgl/MongolText label .style (m/TextStyle
                                              .fontSize 16
                                              .fontWeight (m/FontWeight.w500)
                                              .color text-color))
                (m/Align
                 .alignment m/Alignment.center
                 .child (m/Text (str (validate-stat-value value))
                                .style (m/TextStyle .fontSize 22 .fontWeight (m/FontWeight.bold)
                                                    .color text-color)))]))))

(defn stats-panel []
  (f/widget
   :watch [{note-count :note-count 
            tag-count :tag-count 
            active-days :active-days
            total-blocks :total-blocks} stats/stats-state]
   (m/Padding
    .padding (m/EdgeInsets.symmetric .vertical 16 .horizontal 48)
    .child
    (m/Column
     .mainAxisAlignment m/MainAxisAlignment.center
     .children
     [(m/Row
       .mainAxisAlignment m/MainAxisAlignment.spaceBetween
       .children [(stat-item "Notes" note-count)
                  (stat-item "Tags" tag-count)
                  (stat-item "Active" active-days)
                  (stat-item "Blocks" total-blocks)])]))))