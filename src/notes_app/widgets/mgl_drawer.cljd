(ns notes-app.widgets.mgl-drawer
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [electric-sync.state :as es-state]
   [electric-sync.token :as token]
   [notes-app.utils.navigator :as navigator]
   [notes-app.widgets.mgl-favorite-list :as fal]
   [cljd.flutter :as f]))

(defn mgl-left-drawer []
  (f/widget
   :context ctx
   :get [m/Navigator]
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (-> theme .-colorScheme)
         bg-color (-> color-scheme .-primaryContainer)
         scaffold-bg-color (-> color-scheme .-surface)]
   :watch [uid es-state/uid]
   (m/Drawer)
   (m/SafeArea)
   (m/Container
    .decoration (m/BoxDecoration
                 .color scaffold-bg-color))
   (m/Column
    .children
    [(m/Container
      .padding (m/EdgeInsets.symmetric .vertical 20 .horizontal 16)
      .decoration (m/BoxDecoration
                   .color bg-color)
      .child (m/Row
              .mainAxisAlignment m/MainAxisAlignment.spaceBetween
              .children
              [(m/CircleAvatar
                .radius 20
                .backgroundColor (m/Colors.grey 400)
                .child (m/IconButton
                        .icon (m/Icon m/Icons.menu)
                        .onPressed #(dart:core/print "Open avatar")))
               (m/IconButton
                .icon (m/Icon m/Icons.settings)
                .onPressed (fn []
                             (dart:core/print "Open Settings")))]))
     (m/Divider .height 1)
     (m/Expanded
      .child
      (m/Column
       .children
       [(m/Flexible
         .flex 1
         .child
         (m/SingleChildScrollView
          .scrollDirection m/Axis.horizontal
          .child
          (m/Row
           .mainAxisAlignment m/MainAxisAlignment.start
           .children
           [(if (empty? uid)
              (mgl/MongolListTile
               .leading (m/Icon m/Icons.login)
               .title (mgl/MongolText "Login")
               .onTap (fn []
                        (-> m/Scaffold (.of ctx) .closeDrawer)
                        (navigator/navigate-to-screen navigator "/login")))
              (m/SizedBox))
            (mgl/MongolListTile
             .leading (m/Icon m/Icons.info_outline)
             .title (mgl/MongolText "About")
             .onTap (fn []
                      (navigator/navigate-to-screen navigator "/about")))
            (mgl/MongolListTile
             .leading (m/Icon m/Icons.help_outline)
             .title (mgl/MongolText "Help")
             .onTap (fn []
                      (navigator/navigate-to-screen navigator "/help")))
            (if-not (empty? uid)
              (mgl/MongolListTile
               .leading (m/Icon m/Icons.logout)
               .title (mgl/MongolText "Logout")
               .onTap (fn []
                        (-> m/Scaffold (.of ctx) .closeDrawer)
                        (token/clean)))
              (m/SizedBox))])))
        (m/Divider .height 1)
        (m/Flexible
         .flex 2
         .child (fal/fa-list))]))])))