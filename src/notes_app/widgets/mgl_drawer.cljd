(ns notes-app.widgets.mgl-drawer
  (:require
   ["package:flutter/material.dart" :as m]
   [notes-app.widgets.mgl-calendar-bar :as mgl-calendar-bar]
   [notes-app.widgets.mgl-chip :as mgl-chip]
   [notes-app.widgets.mgl-menu-bar :as mgl-menu-bar]
   [notes-app.widgets.mgl-stats-widget :as mgl-stats-widget]
   [notes-app.states.search :as search]
   [cljd.flutter :as f]))

(declare often-used-tags often-search-query)

(defn mgl-left-drawer []
  (f/widget
   :context ctx
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (-> theme .-colorScheme)
         bg-color (-> color-scheme .-primaryContainer)
         scaffold-bg-color (-> color-scheme .-surface)]
   (m/Drawer)
   (m/SafeArea)
   (m/Container
    .decoration (m/BoxDecoration
                 .color scaffold-bg-color))
   (m/Column
    .children
    [(m/Container
      .padding (m/EdgeInsets.symmetric .vertical 20 .horizontal 16)
      .decoration (m/BoxDecoration
                   .color bg-color)
      .child (m/Row
              .mainAxisAlignment m/MainAxisAlignment.spaceBetween
              .children
              [(m/CircleAvatar
                .radius 20
                .backgroundColor (m/Colors.grey 400)
                .child (m/IconButton
                        .icon (m/Icon m/Icons.menu)
                        .onPressed #(dart:core/print "Open avatar")))
               (m/IconButton
                .icon (m/Icon m/Icons.settings)
                .onPressed (fn []
                             (dart:core/print "Open Settings")))]))
     (m/Divider .height 1)
     (m/Expanded
      .flex 1
      .child
      (mgl-stats-widget/stats-panel))
     (m/Divider .height 1)
     (m/Expanded
      .flex 2
      .child
      (mgl-menu-bar/mgl-menu-bar))])))

(defn mgl-right-drawer []
  (f/widget
   (m/Drawer)
   (m/SafeArea)
   (m/Padding
    .padding (m/EdgeInsets.all 16))
   (m/Column
    .children
    [(mgl-calendar-bar/mgl-calendar-bar)
     (m/Divider .height 1)
     (m/Expanded
      .child (m/Padding
              .padding (m/EdgeInsets.symmetric .vertical 12)
              .child (often-used-tags)))
     (m/Divider .height 1)
     (m/Expanded
      .child (m/Padding
              .padding (m/EdgeInsets.symmetric .vertical 12)
              .child (often-search-query)))])))
              
(defn often-used-tags []
  (f/widget
   :watch [{often-used-tags :often-used-tags} search/search-state]
   (m/Wrap
    .direction m/Axis.vertical
    .spacing 8
    .children (map (fn [s]
                     (mgl-chip/mgl-chip s #(dart:core/print (str "suggestion: " s))))
                   often-used-tags))))
              
(defn often-search-query []
  (f/widget
   :watch [{often-search-queries :often-search-queries} search/search-state]
   (m/Wrap
    .direction m/Axis.vertical
    .spacing 8
    .children (map (fn [s]
                     (mgl-chip/mgl-chip s #(dart:core/print (str "suggestion: " s))))
                   often-search-queries))))