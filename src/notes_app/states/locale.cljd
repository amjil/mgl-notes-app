(ns notes-app.states.locale
  (:require
   ["package:flutter/material.dart" :as m]
   [notes-app.services.pref :as pref]
   [notes-app.utils.i18n :as i18n]))

(def locale-preference-key "app_locale_code")
(def system-selection :system)

(def locale-options
  [{:selection system-selection
    :label-key :language/system
    :locale nil}
   {:selection "mn"
    :label-key :language/mongolian
    :locale (m/Locale. "mn")}
   {:selection "mn-Mong"
    :label-key :language/mongolian-traditional
    :locale (m/Locale.fromSubtags :languageCode "mn" :scriptCode "Mong")}
   {:selection "zh"
    :label-key :language/chinese
    :locale (m/Locale. "zh")}
   {:selection "en"
    :label-key :language/english
    :locale (m/Locale. "en")}])

(declare selected-option)

(def default-selection "mn-Mong")

(def locale-state
  (atom {:selection default-selection}))

(defn available-options []
  locale-options)

(defn init-locale! []
  (let [saved (await (pref/get-string locale-preference-key))
        selection (:selection (selected-option (or saved default-selection)))]
    (swap! locale-state assoc :selection selection)
    selection))

(defn- persist-selection! [selection]
  (if (= selection system-selection)
    (await (pref/remove-value locale-preference-key))
    (await (pref/set-string locale-preference-key selection)))
  selection)

(defn set-selection! [selection]
  (let [selected (:selection (selected-option (or selection default-selection)))]
    (swap! locale-state assoc :selection selected)
    (persist-selection! selected)))

(defn get-selection []
  (:selection @locale-state))

(defn get-selected-locale []
  (:locale (selected-option (get-selection))))

(defn option-label [ctx option]
  (i18n/t ctx (:label-key option)))

(defn selected-option [selection]
  (let [match (first (filter #(= (:selection %) selection) locale-options))
        default (first (filter #(= (:selection %) default-selection) locale-options))]
    (or match default (first locale-options))))

(defn language-subtitle [ctx]
  (option-label ctx (selected-option (get-selection))))

