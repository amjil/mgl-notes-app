(ns notes-app.screens.editor
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:lifecycle/lifecycle.dart" :as lifecycle]
   [clojure.string :as str]
   [cljd.flutter :as f]
   [notes-app.widgets.mgl-app-bar :as mgl-app-bar]
   [notes-app.utils.note :as note-util]
   [notes-app.utils.navigator :as navigator]
   [notes-app.services.db :as db]
   [notes-app.states.notes :as notes]
   [notes-app.states.search :as search]
   [block-editor.widgets.editor :as editor]))

(defn ^m/Widget screen [ctx]
  (f/widget
   :get [m/Navigator]
   :let [theme (-> m/Theme (.of ctx))
         color-scheme (.-colorScheme theme)]
   (m/Scaffold
    .backgroundColor (.-surface color-scheme)
    .appBar (mgl-app-bar/mgl-editor-app-bar ctx navigator)
    .body
    (lifecycle/LifecycleWrapper
     .onLifecycleEvent (fn [event]
                         (condp = event
                           lifecycle/LifecycleEvent.pop (note-util/save-or-update-note! ctx)
                           ;;  lifecycle/LifecycleEvent.invisible (note-util/save-or-update-note! ctx)
                           nil)
                         (dart:core/print (str "onLifecycleEvent: " event)))
     .child
     (editor/editor {:on-search db/search-notes-by-title
                     :on-note-tap
                     (fn [item]
                       (notes/set-current-note-with-history! item)
                       (note-util/load-note-to-editor!)
                       (navigator/navigate-to-screen navigator "/editor"))
                     :on-tag-tap
                     (fn [x]
                       (let [tag-name (get x "tag")
                             query (some-> tag-name str str/trim)]
                         (when (and query (not (str/blank? query)))
                           (-> (db/get-tag-by-name query)
                               (.then (fn [tag]
                                        (let [tag (.-data tag)]
                                          (notes/set-filter-tag! tag)
                                          (notes/load-notes-paginated! ctx 0)))))
                           (navigator/navigate-to-home navigator))))}))))) 